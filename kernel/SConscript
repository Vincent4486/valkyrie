# SPDX-License-Identifier: BSD-3-Clause
"""
Kernel build configuration.
"""

import os

from SCons.Environment import Environment
from SCons.Script import Dir, Export

from scripts.scons.arch import get_arch_config
from scripts.scons.utility import GlobSources, FindIndex, IsFileName


Import('TARGET_ENVIRONMENT')
TARGET_ENVIRONMENT: Environment

# =============================================================================
# Environment Setup
# =============================================================================

env = TARGET_ENVIRONMENT.Clone()

src_dir = Dir('#kernel').abspath
arch = TARGET_ENVIRONMENT['arch']
arch_dir = os.path.join(src_dir, 'arch', arch)
linker_script = os.path.join(arch_dir, 'boot', 'linker.ld')

# Architecture-specific flags (kernel-only)
arch_config = get_arch_config(arch)
env.Append(
    ASFLAGS=arch_config.get('asflags', []),
    CCFLAGS=arch_config.get('ccflags', []),
    LINKFLAGS=arch_config.get('linkflags', []),
)

# Kernel-specific compiler flags
env.Append(
    CCFLAGS=[
        '-ffreestanding',       # No hosted environment
        '-nostdlib',            # No standard library (kernel-only)
        '-fno-stack-protector', # No stack protection (requires libc)
        '-fno-builtin',         # Don't use compiler builtin replacements for libc functions
        '-Wall',
        '-Wextra',
    ],
    LINKFLAGS=[
        '-nostdlib',
        '-Wl,-T', linker_script,
        '-Wl,-Map=' + env.File('core.map').path,
        '-Wl,-z,relro,-z,now',   # Security: read-only relocations
        '-Wl,-z,noexecstack',    # No executable stack
        '-Wl,--as-needed',
        '-Wl,--export-dynamic',
    ],
    CPATH=[src_dir, '#include'],
    CPPPATH=[src_dir, '#include'],
    ASFLAGS=[
        '-I', arch_dir,
        '-g',
        '-Wa,--noexecstack',
    ],
    LIBS=['gcc']
)


# =============================================================================
# Source Files
# =============================================================================

sources = GlobSources(src_dir)
objects = env.Object(sources)


# =============================================================================
# C Runtime Objects
# =============================================================================

# Extract crti.o and crtn.o for proper linking order
obj_crti = objects.pop(FindIndex(objects, lambda x: IsFileName(x, 'crti.o')))
obj_crtn = objects.pop(FindIndex(objects, lambda x: IsFileName(x, 'crtn.o')))

# Build with proper CRT ordering: crti, crtbegin, ..., crtend, crtn
toolchain_libgcc = env['TOOLCHAIN_LIBGCC']
objects = [
    obj_crti,
    os.path.join(toolchain_libgcc, 'crtbegin.o'),
    *objects,
    os.path.join(toolchain_libgcc, 'crtend.o'),
    obj_crtn,
]


# =============================================================================
# Build Target
# =============================================================================

core = env.Program(env['kernelName'], objects)

Export('core')

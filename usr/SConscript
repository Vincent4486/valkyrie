# SPDX-License-Identifier: AGPL-3.0-or-later

import os

from SCons.Environment import Environment
from SCons.Script import Export


Import('TARGET_ENVIRONMENT')
TARGET_ENVIRONMENT: Environment

# Configure musl library and include paths for all usermode modules
musl_include = os.path.join(str(TARGET_ENVIRONMENT['TOOLCHAIN_PREFIX']), 'usr', 'include')
musl_lib = os.path.join(str(TARGET_ENVIRONMENT['TOOLCHAIN_PREFIX']), 'usr', 'lib')

# Create a usermode environment with musl support
usermode_env = TARGET_ENVIRONMENT.Clone()
usermode_env.Append(
    CPATH=[musl_include],
    CPPDEFINES=['_GNU_SOURCE'],
    CPPPATH=[musl_include],
    LINKFLAGS=[
        '-L', musl_lib,
    ],
    LIBS=['c', 'gcc']
)

usermode_env['SHLIBPREFIX'] = 'lib'
usermode_env['SHLIBSUFFIX'] = '.so'

# Export for use in sub-SConscripts
Export('usermode_env')
Export('musl_include')
Export('musl_lib')

# Build libmath and libdisplay libraries first so core can link them statically
SConscript('libmath/SConscript', variant_dir='libmath_build', duplicate=0)
SConscript('sh/SConscript', variant_dir='sh_build', duplicate=0)

# Import libmath library and sh executable
Import('libmath_module')
Import('sh_module')

# Separate user libraries (shared objects) and applications (executables)
user_libraries = [libmath_module]
user_applications = [sh_module]

Export('user_libraries')
Export('user_applications')
